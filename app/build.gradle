apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.joseph.mailme"
        minSdkVersion 21
        targetSdkVersion 26
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
    }

    buildTypes {
        final ADMOB_APP_ID = "ca-app-pub-8775592523731654~1080626197"
        final ADMOB_UNIT_ID = "ca-app-pub-8775592523731654/5454985470"

        debug {
            buildConfigField "String", "ADMOB_APP_ID", "\"$ADMOB_APP_ID\""
            buildConfigField "String", "ADMOB_UNIT_ID", "\"$ADMOB_UNIT_ID\""

        }

        release {
            buildConfigField "String", "ADMOB_APP_ID", "\"$ADMOB_APP_ID\""
            buildConfigField "String", "ADMOB_UNIT_ID", "\"$ADMOB_UNIT_ID\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '26.1.0'
    final CONSTRAINT_LAYOUT_VERSION = '1.0.2'
    final DAGGER_VERSION = '2.12'
    final GLIDE_VERSION = '3.7.0'
    final RXKOTLIN_VERSION = '2.0.0-RC3'
    final RXANDROID_VERSION = '2.0.1'
    final TIMBER_VERSION = '4.1.2'
    final ESPRESSO_VERSION = '2.2.2'
    final JUNIT_VERSION = '4.12'
    final EVENTBUS_VERSION = '3.0.0'
    final GOOGLE_PLAY_SERVICES_VERSION = '11.6.0'
    final LEAKCANARY_VERSION = '1.5.4'

    // Kotlin stdlib
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Android Support
    implementation "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-fragment:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support.constraint:constraint-layout:$CONSTRAINT_LAYOUT_VERSION"
    implementation "com.google.android.gms:play-services-ads:$GOOGLE_PLAY_SERVICES_VERSION"

    // Rx
    implementation "io.reactivex.rxjava2:rxandroid:$RXANDROID_VERSION"
    implementation "io.reactivex.rxjava2:rxkotlin:$RXKOTLIN_VERSION"

    // Dagger 2 with Android
    implementation "com.google.dagger:dagger-android:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"
    compileOnly "com.google.dagger:dagger:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    // Fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true;
    }

    // Misc
    implementation "com.jakewharton.timber:timber:$TIMBER_VERSION"
    implementation "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    implementation project(':colourslibrary')
    implementation "org.greenrobot:eventbus:$EVENTBUS_VERSION"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$LEAKCANARY_VERSION"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"

    // Unit tests
    testImplementation "junit:junit:$JUNIT_VERSION"
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.easytesting:fest-assert-core:2.0M10"

    // Espresso
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

